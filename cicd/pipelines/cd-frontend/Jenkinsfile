/**
 * Author: Leandro Beretta <lberetta@redhat.com>
 */

try {
    timeout(time: 10, unit: 'MINUTES') {

        node() {

            stage("Choose Version") {
                env.version = selectVersion("app", "DevSnapshot");
            }

            def newTag = "TestingCandidate-" + env.version

            openshiftTag alias: "false",
                    destStream: "app",
                    destTag: newTag,
                    destinationNamespace: "consum-dev",
                    namespace: "consume-dev",
                    srcStream: "app",
                    srcTag: "DevSnapshot-" + env.version,
                    verbose: "true"

            stage("Deploy to Test") {
                sh "oc project consum-test"
                sh "oc patch dc app --patch '{\"spec\": { \"triggers\": [ { \"type\": \"ImageChange\", \"imageChangeParams\": { \"containerNames\": [ \"app\" ], \"from\": { \"kind\": \"ImageStreamTag\", \"namespace\": \"consum-dev\", \"name\": \"app:TestingCandidate-$version\"}}}]}}' -n consum-test"

                openshiftDeploy depCfg: "app",
                        namespace: "consum-test"
            }

            stage("Integration Test") {
                // Complete this section with your actual integration tests

                newTag = "ProdReady-" + env.version

                openshiftTag alias: "false",
                        destStream: "app",
                        destTag: newTag,
                        destinationNamespace: "consum-dev",
                        namespace: "consum-dev",
                        srcStream: "app",
                        srcTag: "TestingCandidate-" + env.version,
                        verbose: "false"
            }

            stage('Deploy Pre-Prod?') {
                input "Deploy new version to Pre-Production?"
            }

            // Blue/Green

            def dest = "app-green"
            def active = ""

            stage("Deploy to Pre-Prod") {

                sh "oc project consum-prod"
                sh "oc get route blue-green -n consum-prod -o jsonpath='{ .spec.to.name }' > active"

                active = readFile("active").trim()

                if (active == "app-green")
                    dest = "app-blue"

                sh "oc patch dc ${dest} --patch '{\"spec\": { \"triggers\": [ { \"type\": \"ImageChange\", \"imageChangeParams\": { \"containerNames\": [ \"$dest\" ], \"from\": { \"kind\": \"ImageStreamTag\", \"namespace\": \"consum-dev\", \"name\": \"app:ProdReady-$version\"}}}]}}' -n consum-prod"

                openshiftDeploy depCfg: dest,
                        namespace: 'consum-prod'
            }

            stage("Switch Version?") {
                input "Switch to new version?"
            }

            sh "oc patch route blue-green -n consum-prod -p '{ \"spec\" : { \"to\" : { \"name\" : \"$dest\" } } }'"

            stage("Finish Deployment") {
                rollback = input message: "Rollback to previous version?",
                        parameters: [choice(choices: "Yes\nNo", description: '', name: 'Rollback')]
            }

            if (rollback.equals('Yes')) {
                sh "oc patch route blue-green -n consum-prod -p '{ \"spec\" : { \"to\" : {\"name\" : \"$active\" } } }'"
                echo "Deployment rolled back successfully"
            } else {
                echo "Deployment finished successfully"
            }
        }
    }
} catch (err) {
    echo "Caught: ${err}"
    currentBuild.result = 'FAILURE'

    throw err
}

def getVersionFromPom(pom) {
    def matcher = readFile(pom) =~ '<version>(.+)</version>'
    matcher ? matcher[0][1] : null
}

def selectVersion(imageName, tagPrefix) {
    def selectedTag = ""

    sh "oc get istag -n consum-dev --sort-by='{.metadata.name}'| grep ${imageName}| grep ${tagPrefix} | awk '{print \$1}' | awk -F ':' '{print \$2}' | awk -F '-' '{print \$2}' | tail -5 > tags"

    def tags = readFile("tags").trim()

    selectedTag = input message: "Choose version",
            parameters: [choice(choices: tags, description: '', name: 'Versions')]

    return selectedTag
}
